//This query calculates the average response time (Mean Time to Respond) for incidents, displaying the time in the format DD:HH:MM
//"Time to respond" refers to the time difference between when an incident is first generated (appears) in Sentinel and when it is first assigned to a responder.

let Starttime= datetime(2024-07-01 00:00:00);
let Endtime=datetime(2024-07-07 23:59:59.9);
// Find the first time each incident appeared in Sentinel
let FirstSeen=
SecurityIncident
| where CreatedTime >= Starttime and CreatedTime <= Endtime
| summarize arg_min(TimeGenerated, *) by IncidentNumber
| extend FirstSeenAtSentinel=TimeGenerated
| project FirstSeenAtSentinel, IncidentNumber;
// Query incidents within the specified time range and filter for assigned incidents
SecurityIncident
| where CreatedTime >= Starttime and CreatedTime <= Endtime
| where parse_json(Owner).assignedTo <> ''
// Find the first time each incident was assigned
| summarize arg_min(TimeGenerated, *) by IncidentNumber
| extend FirstTimeAssiged=TimeGenerated
// Join with the FirstSeen dataset to get both the initial appearance and first assignment times
| join kind=inner FirstSeen on IncidentNumber
// Calculate the response time in seconds (difference between first assigned time and first seen time)
| extend ResponseTimeSeconds=datetime_diff("second", FirstTimeAssiged, FirstSeenAtSentinel)
// Calculate the average response time in seconds for each severity level
| summarize ResponseTimeSecondsAvg=avg(ResponseTimeSeconds) by Severity
// Convert the average response time from seconds into days, hours, minutes, and seconds
| extend seconds=ResponseTimeSecondsAvg
| extend days= bin(seconds / (24 * 3600),1)
| extend seconds= seconds % (24 * 3600)
| extend hours= bin((seconds/3600), 1)
| extend seconds = (seconds % 3600)
| extend minutes = bin((seconds / 60),1)
| extend seconds = bin((seconds % 60),1)
// Format the response time as DD:HH:MM:SS for easier readability
| extend MeanTimeToRespond=strcat(toint(days),":",toint(hours),":",toint(minutes),":",toint(seconds)) //MeanTimeToRespond in this format DD:HH:MM:SS
| project Severity, MeanTimeToRespond, days, hours, minutes, seconds
| sort by Severity
